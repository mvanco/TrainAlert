apply plugin: 'com.android.application'

/**
 * LOGS (boolean) - If logs should be printed.
 * ACTION_BAR_BLOCKED (boolean) - If action bar should be password protected.
 * USE_SIMULATED_REPOSITORY (boolean) - If repository is mocked for testing.
 * REST_BASE_URL (String) - URL address of application server for this application.
 * USE_OFFLINE_MAPS (boolean) - If application should obtain map tiles from custom offline server.
 * GPS_TIME_INTERVAL (int) - Time between two gps coordinates measurements in milliseconds, GetLocation service polling.
 */

android {
    signingConfigs {
        config {
            keyAlias 'trainalert'
            keyPassword 'Ohzi4lengo'
            storeFile file('C:/Users/Matus/AndroidStudioProjects/TrainAlert/extras/keystore/trainalert.jks')
            storePassword 'Ohzi4lengo'
        }
    }
    compileSdkVersion 27
    defaultConfig {
        applicationId "cz.intesys.trainalert"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            buildConfigField "boolean", "LOGS", "true"
        }
        release {
            buildConfigField "boolean", "LOGS", "false"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            renderscriptDebuggable false
        }
    }
    flavorDimensions "client", "server"
    productFlavors {
        normal {
            dimension "client"
            buildConfigField "boolean", "ACTION_BAR_BLOCKED", "true"
        }
        unblocked {
            dimension "client"
            buildConfigField "boolean", "ACTION_BAR_BLOCKED", "false"
        }

        dev {
            dimension "server"
            buildConfigField "boolean", "USE_SIMULATED_REPOSITORY", "true"
            buildConfigField "String", "REST_BASE_URL", "\"-\""
            buildConfigField "String", "OFFLINE_MAPS_BASE_URL", "\"-\""
            buildConfigField "boolean", "USE_OFFLINE_MAPS", "false"
            buildConfigField "int", "GPS_TIME_INTERVAL", "7000"
        }
        staging {
            dimension "server"
            buildConfigField "boolean", "USE_SIMULATED_REPOSITORY", "false"
            buildConfigField "String", "REST_BASE_URL", "\"http://192.168.16.36:3001\""
            buildConfigField "String", "OFFLINE_MAPS_BASE_URL", "\"http://tile.intesys.cz\""
            buildConfigField "boolean", "USE_OFFLINE_MAPS", "false"
            buildConfigField "int", "GPS_TIME_INTERVAL", "2000"
        }
        production {
            dimension "server"
            buildConfigField "boolean", "USE_SIMULATED_REPOSITORY", "false"
            buildConfigField "String", "REST_BASE_URL", "\"http://192.168.0.10:5000\""
            buildConfigField "String", "OFFLINE_MAPS_BASE_URL", "\"http://192.168.0.10:5000\""
            buildConfigField "boolean", "USE_OFFLINE_MAPS", "true"
            buildConfigField "int", "GPS_TIME_INTERVAL", "500"
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:support-vector-drawable:27.1.1'

    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'org.osmdroid:osmdroid-android:5.6.5'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.okhttp3:okhttp:3.8.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.1'
    implementation 'com.google.code.gson:gson:2.8.1'
    implementation 'com.google.dagger:dagger:2.8'
    implementation 'javax.inject:javax.inject:1'
    implementation 'io.reactivex.rxjava2:rxjava:2.0.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.0'
    implementation 'com.jakewharton:butterknife:8.4.0'

    compileOnly 'javax.annotation:jsr250-api:1.0'

    annotationProcessor 'com.google.dagger:dagger-compiler:2.8'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}